sourceSets {
    intTest
}

configurations {
    intTestOfflineRepo
}


// For a single test, you can run "gradle intTest --tests <test name> "
task intTest(type: Test) {
    description = "Runs the plugin's integration tests"
    group = "verification"

    mustRunAfter "test"
    inputs.files sourceSets.main.output.classesDirs
    inputs.files sourceSets.main.output.resourcesDir

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath

    dependsOn ':testfixtures-offline-repo:buildOfflineRepositories'
    systemProperties OFFLINE_REPO: offlineRepoRoot.absolutePath
}

check {
    dependsOn intTest
}

pluginManager.withPlugin('jacoco') {
    jacocoTestReport {
        executionData intTest
        mustRunAfter intTest
    }
}

pluginManager.withPlugin('java-gradle-plugin') {
    gradlePlugin {
        testSourceSets sourceSets.intTest
    }
}